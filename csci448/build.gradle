plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

group = 'com.github.jpaoneMines'
version = "0.1.2"

android {
    namespace 'edu.mines.csci448'
    compileSdk 33

    defaultConfig {
        minSdk 29
        targetSdk 33
        versionCode 3
        versionName "$version"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.1'
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = "$group"
                artifactId = 'csci448'
                version = "$version"

                from components.release
                pom {
                    name = 'CSCI 448 Library'
                    description = 'CS@Mines CSCI 448 Library'
                    url = 'https://cs-courses.mines.edu/csci448/'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/license/mit/'
                        }
                    }
                    developers {
                        developer {
                            id = 'jpaoneMines'
                            name = 'Dr. Jeffrey Paone'
                            email = 'jpaone@mines.edu'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                name = 'csci449'
                url = uri("file://${project.buildDir}/repo")
            }
        }
    }
}

dependencies {
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation 'androidx.compose.material:material:1.4.2'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'
}